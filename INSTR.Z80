todo  = 1
may_optim = 1
may_crunch = 1
dev_checks' = 1         ; new checks
; Routine for each type of intruments:
      ; - instr_default

;2026 Jan 5: instr_null must cut fx

; Limitations:
   ; - only works for fixed /regs/ address XX00. 

      IMPORT "plyconf.i"
      IMPORT "macro.i"

      IF ayane
      IMPORT "const.i"  ; instr_head_size
      IMPORT "memmap.i" ; BK_INSTR, instr_idx, psg_regs
      IMPORT "assert.o"
      IMPORT "periods.o"
;!! periods don't pad itself, to get the correct binary size
;!! when assembling ply.o with ayane=0
      FILL -$ AND &FF,&F7
      IF sfx
      IMPORT "timer.o"  ; for ret_sp, fx_end
      END
      ELSE
psg_regs = compiled_psg_regs
 ; instr is imported at start of ply.o (to ensure confinement)
 ; yet periods must go in its own &100 buffer at the end of code
      IF $ - $$
  !! unexpected
      END
save_org = $
      ORG compiled_periods
      IMPORT "periods.o"
      ORG save_org
      END

r6    = psg_regs+6
r7    = psg_regs+7
r11_12 = psg_regs+11
r13   = psg_regs+13
r14   = psg_regs+14     ; Reset by ply before iteration

      IF ayane
      IF $ AND &FF:ELSE
; LSB cannot be null: 00 used to flag empty instr.
; Hmm... we could use instr_null instead? nevermind.
; For compiled version: no such issue!
          nop
      END
      END

loop_bit = 7
fail  = &BE00

      MACRO CHECK_CONFINE
; For now constraint also in-tool
; Can aleviate that by storing ID instead of LSB
; Other mitigation possible: put instr_default last
      IF $/&0100 - instr_code/&0100
  !!! error confine 
      END
      ENDM

      MACRO INC_HL_INSTR
      IF ayane
          inc hl
      ELSE
          inc l         ; kimd: confined instruments
      END
      ENDM

type_instr_null = 0
type_instr_mute = 1     ; todo 
type_instr_default = 2

instr_code              ; MSB needed by ply.o  +check by fx.o

VARIABLE_SIZE = -1      ; For rows

; --------------------------------------------------------
      MACRO SIGN type,paramsize,rowsize
      IF ayane
; Order used by chung.Song_select_instr
      IF paramsize AND &FC ; >3
  !! for more than 3 params, make sure to export them
  ; as the are only 3 byte free in the header
      END

      BYTE rowsize      ; 0: no table associated. -1: variable
      BYTE paramsize
      BYTE type
      END
      ENDM

; --------------------------------------------------------
          SIGN(type_instr_null,0,0)
instr_null
;Required in both in-tool and compiled,
; as ply inconditionaly jump to instr routine.
;When no current instr, come here.
;Also fallback for undefined instr.
          pop hl        ; consume param
      IF use_flag_channel
          pop hl
      END
          pop hl        ; consume regs+vol
      IF sfx
;Needd to cut fx, as instr_null is used for 'CLR'
          SUB_TM(9+3-3) ;-3: no ld sp,0 (from fx_end)
          jp fx_end_
      ELSE
          RET_ROUT(9)
      END

      IF use_instr_end
_instr_end
; After final row: don't change regs and cut fx.
; Note it differs from instr_null.
; Also, this is not an instr type.
; Routine placed here to be reachable by jr
tm1_  = 8               ; from .exec to jr instr_end (taken)
          pop hl        ; consume regs+vol
          pop hl        ; consume regs+mask            
      IF sfx
          SUB_TM(tm1_+6+3-3) ;-3: no ld sp,0 (from fx_end)
          jp fx_end_
      ELSE
          RET_ROUT(tm1_+6)
      END
      END
; --------------------------------------------------------
      IF use_instr_default ; Not used to compile minimala.chp
          SIGN(type_instr_default,0,VARIABLE_SIZE)
instr_default
; Same API than /.exec/
          CHECK_CONFINE()
; First iteration
      IF ayane
 ; replace jp for next iterations
          EX_AF'()
          ld hl,.exec:push hl:pop hl
 ; get row pnt from header.
          pop hl
          ld a,(hl):inc hl ; !! cannot use DE (= pitch)
          ld h,(hl):ld l,a
      IF dev_checks
; If wrong, typically BK_INSTR wasn't connected
          ld a,h:cp tables/&0100:call c,mess
          cp tables_/&0100:call nc,mess
; no loop in header.
          bit loop_bit,h:call nz,mess
          SUB_TM''(16)
      END
          EX_AF'()
          SUB_TM_CALL(26) ; from instr_default until .exec
          push hl
;enchaine with .exec
.exec
      ELSE
;compiled: no indirection
 !! must handle instrument end then.
      END
; In:               
      IF use_volume
      ;  B = Volume (row mitigated by general track volume)
      END
      ;  C = Note (transposed)
      ; DE = accumulated pitch
      ; SP = Points on: - instr parameter
                      ; - Pnt to volume register (eg regs+8)
                      ; - Regs + channel masked (eg %110110 for A)  
      ;  A = TM
;/tm1_/ placed in _instr_end to avoid forward reference
          pop hl        ; Instr table
      IF use_instr_end
          inc h:dec h:jr z,_instr_end
      END
; Follow next link.
; Handling that first allows to skip optional field at the end for free
                          ; and use L or H to read last 
      IF ayane
tm1   = tm1_-1 + 14+7+48 ; Until jr ok_noise (taken)
          ld ixl,c      ; Free C (also, needed later for hard env)
          ld ixh,b      ; Free B
          ld c,(hl):inc hl
          ld b,(hl):inc hl
          res loop_bit,b
; Must be done before push
; Might trigger timer early,
; it's still better than delaying it for too long
          SUB_TM_CALL(tm1)
          push bc       ; for next time
          pop bc        ; skip
      ELSE
;For compiled version, relative link (confined in &100)
      IF use_volume
          ld ixh,b
      END
          ld a,(hl)
          add l
      IF use_phrase_pitch
!! todo, cannot use de
      END
          ld e,a
          ld d,h
          push de
          pop de        ; skip
          inc l
      END

; Start by arpeggio and pitch, since we must do that anyway
; Except if absolute value in instr, but:
   ; - We don't handle those anyway :)
          EX_AF'()
      IF ayane
          ld a,ixl
      ELSE
          ld a,c
      END
      IF default_use_arp
          add (hl):INC_HL_INSTR()
      END
; Compute period now, since we instr pitch comes next
; Computed period is kept in BC:
; we delay poking it in period registers, 
; as we want to handle r7/r6 first: free registers for more complex
                                  ; stuff afterward (hard_env) 
                     ; vol last:  
; Using BC allow to free DE for easier subsequent code (ex de,hl)
          ld c,a
          ld b,periods/&0100
      IF use_phrase_pitch
; DE= pitch set by fx (vibrato, portamento)
      IF default_use_pitch
          ld a,(bc):add e:jr nc,$+3:inc d
          add (hl):INC_HL_INSTR():ld e,a
          inc b
          ld a,(bc):adc d
          add (hl):INC_HL_INSTR():ld b,a:ld c,e
      ELSE
!! todo
      END
      ELSE
      IF default_use_pitch
          ld a,(bc):add (hl):INC_HL_INSTR():ld e,a
          inc c
          ld a,(bc):adc (hl):INC_HL_INSTR():ld b,a:ld c,e
      ELSE
          ld a,(bc):ld e,a
          inc c
          ld a,(bc):ld b,a:ld c,e
      END
      END

; r7
      IF use_flag_channel
          pop de        ; Mask channel / noise for current channel

; Flags channel/noise (copied in each channel)
          ld a,(hl)
          or e
          ex de,hl
          ld l,r7 AND &FF
          and (hl)
          ld (hl),a
          ex de,hl
          bit 3,(hl)    ; any bit 3,4,5 would work
          inc hl        ; no INC_HL_INSTR as we must preserve Z
          jr nz,.ok_noise

;---- Noise register: don't set if 0 (piloted from elsewhere)
          dec e         ; reg 6
          ld a,(de)     ; current value
          ld e,(hl)
          inc e:dec e:jr z,$+3:ld a,e
          ld (r6),a
          SUB_TM''(14-1) ; -1 for tm not taken
;enchaine
      ELSE
; Infer noise flag from noise value  
      IF default_use_noise
      BRK
!! Fixme, flag not set in r7
          ld e,&3F
          ld a,(hl)
          or a
          jr z,.ok_noise
          ld (psg_regs+6),a
          ld e,&07
      END
      END
.ok_noise
          INC_HL_INSTR() ; skip noise value

; Volume, or env_hard:
    ;  xxxx0 -> vol xxxx (F=max volume)
    ; 1yyyy1 -> env hard            
          ld a,(hl):INC_HL_INSTR()
      IF use_hard_env
tm2   = 10              ; until jr no_hard_env (taken)
          rrca
          jr nc,.no_hard_env

;env_type (bit 4 set for retrig)
tm_hard = 17-1          ; until jr abs_hard (taken)
          ld a,(hl):INC_HL_INSTR()
          ld (r13),a
;arp
          ld a,(hl):INC_HL_INSTR()
          cp ARP_ESC_SET
          jr z,.abs_hard

; Applicate arp+pitch to base note
; !!! Don't touch BC:   
    ; hasn't been poked yet
    ; it's still the base to use (well, unused feature now)
; Maybe we could use C for base note, since that's the most important
          add ixl
          ld e,a
          ld d,periods/&0100
; /16 (nops to psg) /16 (psg to env hard) -> /256
          ld a,(de):add (hl) ; Just for carry and bit 7 below
          INC_HL_INSTR()
          ex de,hl
          inc l
          ld l,(hl):jr nc,$+3:inc l
; rounding to closest
          add a         ; if bit7
          ld a,(de):inc de:adc l:ld l,a
          ld h,0
          ld (r11_12),hl
          ld a,&10
          EX_AF'()
          SUB_TM(tm_hard-1+38) ; -1 jr abs_hard not taken
          jr .ok_vol

.abs_hard
; Absolute periode, don't change current period in BC
          ld e,(hl):inc hl
          ld d,(hl):inc hl
          ld (r11_12),de
          ld a,&10
          EX_AF'()
          SUB_TM(tm_hard+20)
          jr .ok_vol

      ELSE
 !!! todo
      END
.no_hard_env
      IF use_volume
tm2'  = 15
          sub ixh:jr nc,$+3:xor a ; a= clipped volume
      ELSE
tm2'  = 10
      END

      IF sfx
          fx0_type - instr_hard_type ** inc hl ; skip hard env params
          ex de,hl
      END
          EX_AF'()
          SUB_TM(tm2')
.ok_vol
tm3   = 16              ; until SUB_CALL_TM
          pop hl        ; last word popped: volume
          EX_AF'()
          ld (hl),a
      IF sfx
          ld (ret_sp),sp
          ld sp,trash+2 ; to use SUB_TM_CALL. SP reset to HL below
          EX_AF'()
          SUB_TM_CALL(tm2+tm3)
      END
tm3'  = 14              ; until RET_ROUT'
          EX_AF'()
 ; period register. Vol 8->0  9->2  10->4
 ; Note: if !! Full 16 bits precisions. Converted on the fly,
     ; Since it's cumbersome to do it here (must preserve hl,de,bc)
          ld a,l:sub 8:add a:ld l,a
          ld (hl),c:inc l
          ld (hl),b

      IF sfx
.special_fx
; Use SP:
   ; - Faster (e.g. sync buzzer 36 nops instead of 54)
   ; - Needed to interupt with timer
     ; Since whole instr routine might take more than 256 nops.
;Note: We could use sp for previous field. Not done for now:
     ; - Don't want to rewrite code (must rewrite ply as well to
         ; pass mask and volume beforehand in one go)
             ; -> No! Actually don't do the channel dispatch here
                    ; but in psg register dispatch
                    ; -> Faster, simpler: as we add virtual registers,
                       ; we need dynamic pointer anyway.
             ; -> Must rewrite to decorelate ply+instr (300hz instr)
     ; - Constraints might change (e.g. custom table for some fields)
     ; - YAGNI     
      IF dev_checks
          inc de:ld a,(de):dec de
          cp timer_code/&0100:call c,&BE00
          SUB_TM''(11)
      END
          ex de,hl
          ld sp,hl
          RET_ROUT'(tm3') ; fx_end or sync_buzzer
      ELSE
          ret
      END
      END

instr_code_             ; for check by fx.o

; ------------ Helpers ------------

      IF ayane

      IF 1-sfx
 !!! sfx expected to be on in ayane
  ; we use JP HL' here!
      END

      MACRO CHECK_DI
      IF dev_checks
          call _check_di
      END
      ENDM

instr_setup
;----------
; For piano and instrnrt
; IN: L = instr number > 0
;OUT: N/A
          ASSERT(instr_head_size == 8)
          ASSERT(instr_idx AND &07FF == 0)

          ld h,instr_idx / &0800
          3 ** add hl,hl

          ld bc,&7F00+BK_INSTR:out (c),c

      IF dev_checks'
          ld a,(hl)
          cp instr_default AND &FF:jr z,.ok
          cp instr_null AND &FF:jr z,.ok
      BRK
.ok
      END
          ld de,instr_code_pnt
          ldi
          ex de,hl
; Force MSB in case instr.o change it (post-amorce routine might be
; elsewhere);
          ld (hl),instr_code/&0100:inc hl
          ld (hl),e:inc hl
          ld (hl),d
          ret

MAX_TM = &80

instr_play
;---------
; For piano and instrnrt
; In: !! DI, instr_setup has been called
    ; A = note (0 = C0)
    ; A' = TM
    ;HL' = timer routine
;Out: A = TM updated

;Note: cannot use ret_sp since it will be overwritten 
     ; from instr to timer.
          add a         ; instr expect note *2 (index in periods table)
          ASSERT(c0 AND &FF == 0)
          ld c,a        ; note 
          ld b,0        ; volume (substracted)
          ld de,0       ; shift
          ld a,&3F:ld (r7),a
; Mimic ply.o pre-loop (for fx management)
; For now, don't inline, to trigger timer at RET point.
          ex af,af
          ld (save_sp+1),sp
          ld sp,instr_code_pnt
 ; jump to instr code and then back at play_return
          RET_ROUT(23+6) ;+6: ld sp,n:jp n
play_return
save_sp   ld sp,0
          ret

      IF dev_checks
_check_di
          SUB_TM''(8+13)
          push af
          ld a,i:call pe,fail
          pop af
          ret
      END

instr_code_pnt WORD instr_code ; Fixed MSB
instr_param WORD 
      IF use_flag_channel
      WORD psg_regs+[&3F XOR &12] ; mask channel-B
      END
      WORD psg_regs+9   ; vol channel-B
      WORD play_return
          3 ** WORD &BE00 ; sanity
      END

; ---------------------------------
      IF sfx
trash WORD 
      END

hi


