; Tests ply.o            
do_profile = 1
; 2026
; --- alpha2 --
  ; Jan     
      ;17 Fix assemble when do_profile = 0
        ; Fix nrt_setup_play. init_timer was never called!?
; --- alpha1 --
      ; 5 Add test_clr_fx  
      ; 4 Add test_clr
; 2025 
; --- v0.0au ---
  ; Nov  9 Add test_endoftrack
         ; s/nrt_play/nrt_play_from_start
; --- v0.0at ---
  ; Oct 16 Use "macro.i"
         ; Fix nrt_play (when do_profile = 0)
; --- v0.0aq ---
  ; Sep 27 Fix changed api
         ; c-0 is now 0
; --- v0.0ac ---
  ; Mar 05

      IMPORT "const.i"
      IMPORT "memmap.i"
      IMPORT "plyconf.i"
      IMPORT "macro.i"
      IMPORT "import.i"

      ORG code_start
      ENT tests

 ; ensure same location than in ayane.o 
          IMPORT_SHARED_DEPS_ALL()
          IMPORT_SHARED_DEPS() ; Init_chung, New_instr_ayane
 ; For Init_track_module, phrase_select_new_row
          IMPORT_SHARED_DEPS''()
      IMPORT "timer.o"  ; for fx_end, SUB_TM_CALL
      ORG playsong_module
      IMPORT "testlib.o"

track1 = tracks+4       ; track 0 empty (reserved)


; ------------------------------------------------
fail  = &BE00

tests
      IF do_profile
          call test_profiling
          call test_timer_nop
      END
          call test_empty_song
      IF do_profile
          call test_empty_song_no_hook_tm
          call test_empty_song_tm
      END
          call test_empty_phrase
          call test_note_instr
          call test_clr
          call test_clr_fx
          call test_sync_buzzer
          call test_endoftrack
          ret

nrt_init
progress_bar ld hl,&E000
          ld (hl),&55
          inc hl
          ld (progress_bar+1),hl

          call Init_chung
          call Init_track_module
          call New_module ; create 3 empty tracks
          call phrase_reset
; ply uses ex exx and af,af'
          ld hl,&C9FB:ld (&38),hl
          ret


;-----------------------------------------------
      MACRO CHECK_HL_EQ n
          push de
          ld de,n:or a:sbc hl,de:add hl,de:call nz,fail
          pop de
      ENDM

;-----------------------------------------------
test_profiling
; Check test frameword itself
          call nrt_init
          ld ix,.noops
          call check_tm
          ret
.noops
          SUB_TM_CALL(0)
          RET_ROUT(0)


test_timer_nop
; Check test frameword itself (with call to /timer_nop/)
          call nrt_init
          ld ix,.noops_long
          call check_tm
          ret
.noops_long
          ld b,51       ; enough for more than 512 nops
.lp
          SUB_TM_CALL(4)
          djnz .lp

          RET_ROUT(2-1) ; -1 for last djnz

test_empty_song
; After ayane_init.
          call nrt_init
; Test 256 times                   
; Enough to check for spurious RDV (track event mustn't be reached)
; Not too long either
          ld b,0
.lp
          push bc

          di
          call nrt_play_from_start

          ld a,(psg_regs+7)
          cp &3F:call nz,fail

          pop bc
          djnz .lp
          ret

test_empty_song_no_hook_tm
; Check TM once phrase has been set
; -> shorter codepath to verify in case of TM discrepancy

          call nrt_init
          call nrt_play_from_start ; 1st to install phrase
          call tm_play
          ret

test_empty_song_tm
; This version check TM
          call nrt_init
          ld b,0
.lp
          push bc
          call tm_play_from_start
          ld a,(psg_regs+7)
          cp &3F:call nz,fail
          pop bc
          djnz .lp
          ret


test_empty_phrase
;----------------
; -- Empty phrase (well, phrase with one empty row)
          call nrt_init

          xor a:call song_select_new_phrase
; Needed to avoid infinite autoloop
          call phrase_select_new_row:call nc,fail

; --- track with this phrase
          call nrt_phrase0_in_track1

      IF do_profile
          call tm_play_from_start
          call tm_play  ; 2nd iter
      ELSE
          call nrt_play_from_start
          call play
      END
          ret

test_note_instr
;--------------
;simple note with simple instrument
          call nrt_init
          call nrt_dummy_song
check_note_instr
      IF do_profile
          call tm_play_from_start
      ELSE
          call nrt_play_from_start
      END
          ld hl,.ref
          ld de,psg_regs
          ld b,8
          call compare_sized
          ret

.ref
      WORD &EEE4+pitch
      FILL 5,untouched
      BYTE &3E
      BYTE 15
      FILL 2,untouched

test_clr
;-------
; Check instr is propely cut
; Prerequisite: test_note_instr
          call nrt_init
          call nrt_dummy_song
          ld b,0:call instr_set_loop
          ld de,1:call phrase_select_row_at
          call row_clr
;Sanity: replay test for first iter
          call check_note_instr
;Check: volume must be 0
      IF do_profile
          call tm_play
      ELSE
          call play
      END
          ld hl,.ref
          ld de,psg_regs
          ld b,8
          call compare_sized
          ret

.ref
      WORD &EEE4+pitch
      FILL 5,untouched
      BYTE &3F
      BYTE 0
      FILL 2,untouched

test_clr_fx
;----------
; Check fx is propely cut
          call nrt_init
          call dummy_instr_sync
          call nrt_dummy_phrase
          ld b,0:call instr_set_loop
          ld de,3:call phrase_select_row_at ; sync is at row 2
          call row_clr
      IF do_profile
          call tm_play_from_start
          3 ** call tm_play
      ELSE
          call nrt_play_from_start
          3 ** call play
      END
          ld a,(cut_list)
          or a:call z,fail
          ret


test_sync_buzzer
;---------------
; Only check TM counting
          call nrt_init
          call dummy_instr_sync
          call nrt_dummy_song
;repeat test to detect spurious desync
          ld b,20
.lp
          push bc
      IF do_profile
          call tm_play_from_start
          2 ** call tm_play
      ELSE
          call nrt_play_from_start
          2 ** call play
      END
          pop bc
          djnz .lp
          ret

test_endoftrack
;--------------
; track handling should be no-op after end
          call nrt_init
          call nrt_dummy_song
;do setup
      IF do_profile
          call tm_play_from_start
      ELSE
          call nrt_play_from_start
      END
          ld b,nrt_duration * 2
.lp
          push bc
      IF do_profile
          call tm_play
      ELSE
          call play
      END
          pop bc
          djnz .lp
          ret


nrt_dummy_song
; --- dummy instr (TODO? Move in testlib which already have instrs)
                  ;-> YAGNI. It's fine as this
          ld a,1:call New_instr_ayane
          xor a
          ld de,row0
          call helper_setrow
;enchaine
nrt_dummy_phrase
; --- dummy phrase
          xor a:call song_select_new_phrase
nrt_duration = 12
          ld a,nrt_duration
          call phrase_select_new_row:call nc,fail
          ld a,0:call row_set_note
          ld a,1:call row_set_instr
;enchaine
; --- dummy track
nrt_phrase0_in_track1
          ld a,1:call Song_select_track
          xor a
          ld hl,rdv0
          call Set_phrase_at_t
          ret

rdv0  FILL 3,0


pitch = -7
row0  BYTE 0:WORD pitch:BYTE &38,0
          VOL(15)
      FILL fx0_type - [$-row0],0

      IF 1
      WORD fx_null
      FILL instr_fx_size-2,0
      WORD fx_null
      FILL instr_fx_size-2,0
      WORD fx_end
      IF instr_row_size_max - [$-row0]
 !!! incomplete
      END

      ELSE

      WORD fx_end
      IF instr_row_size_max - [$-row0]
 !!! incomplete
      END
      SKIP instr_row_size_max - [$-row0]
      END

compare_sized
          ld a,(de):cp (hl):call nz,&BE00
          inc hl:inc de
          djnz compare_sized
          ret

compare_regs
; less strict comparison:
; e.g. if channel closed, don't check periods

          ld de,psg_regs

          push hl:pop ix
          push de:pop iy

          ld a,(ix+7):cp (iy+7):call nz,&BE00

      MACRO COMPARE_CHAN ch,chbit,exit

          bit chbit,(ix+7):jr nz,exit

          ld a,(ix+ch):cp (iy+ch):call nz,&BE00
          ld a,(ix+ch):or a:jr z,exit

; compare with +/- 3 allowed diff (since vibrato isn't exactly the same)

          push de:push hl
          ld c,(hl):inc hl:ld b,(hl)
          ex de,hl
          ld e,(hl):inc hl:ld a,(hl):and &0F:ld d,a
          ex de,hl
          or a
          sbc hl,bc:jr z,exit-2 ; 0   
          sra h:rr l    ; /2  
          ld a,l:or h:jr z,exit-2 ; Was 1
          ld a,l:dec a:or h:jr z,exit-2 ; 2 or 3
          ld a,l:and h:inc a:call nz,&BE00 ; -1 or -2 

 ;     IF $ - [exit-2]
 ; !! error addr moved
 ;     END
          pop hl:pop de
      ENDM

          COMPARE_CHAN(8,0,cr_noA)

cr_noA
          inc hl:inc de
          inc hl:inc de

          COMPARE_CHAN(9,1,cr_noB)

cr_noB
          inc hl:inc de
          inc hl:inc de

          COMPARE_CHAN(10,2,cr_noC)

cr_noC
          inc hl:inc de
          inc hl:inc de

          ld a,(ix+7):rra
          or (ix+7):rra
          or (ix+7)
          bit 3,a:jr nz,cr_nono

          ld a,(de):cp (hl):call nz,&BE00
cr_nono

      5 ** [inc hl:inc de] ;6 to 11      
          ld a,(de):cp (hl):call nz,&BE00
          inc hl:inc de
          ld a,(de):cp (hl):call nz,&BE00
          inc hl:inc de
          ld a,(de):cp (hl):call nz,&BE00

          ret


nrt_play_from_start
;------------------
; Play first tick
; It doesn't check anything
          call nrt_setup_play
          di
; Reset for profiling
; Cannot be set before since prof.profile do EI + WAIT_VBL
          exx
          ld hl,timer_nop
          exx
          ex af,af
MAX_TM = &80
          ld a,MAX_TM
          call play
          ret

tm_play_from_start
;-----------------     
; reset psg regs for accurate check
          call nrt_setup_play
tm_play
          ld ix,play
          call check_tm
          ret

nrt_setup_play
          ld hl,psg_regs
          ld b,14
untouched = &DD
.raz      ld (hl),untouched:inc l:djnz .raz

          call init_timer
          call Amorce_song
          ret

check_tm
;-------
; In: Ix= routine to check

          di
; Reset for profiling
; Cannot be set before since prof.profile do EI + WAIT_VBL
          exx
; Instr routine might > 256 nops.
          ld d,0        ; MSB cpt
          ld hl,timer_nop
          exx
          ex af,af
MAX_TM = &80
          ld a,MAX_TM   ;load for debug
;      BRK:call play
          ld a,MAX_TM   ;reload for profile
          call profile_ix
          neg
          add a
          add MAX_TM*2-3
; !! review this, wrong when A was 0. time = &80*2 - 3 = &fd,
   ; d musn't be dec in this case
      ;    exx:jr c,$+3:dec d:exx ; propagate carry to MSB
          cp l:call nz,fail
          ld a,h:exx:cp d:call nz,fail:exx
          ret

tmp
          SUB_TM(3)
          ret

timer_nop
;--------         
          add &80       ; reload
          SUB_TM(7)
          inc d         ; MSB TM
          exx
          ret

      SKIP ramlimit - $

; Put at the end to respect ORG order (workaround bug#108)
; No more room befor ramlimit
      ORG free_ram_for_nrt
      IMPORT ":orgrel/prof.o"


