
; Dispatch key -> routine.

ksetkey = &BB27         ; aka km_set_translate
kgetkey = &BB2A         ; aka km_get_translate
ksetshift = &BB2D
ksetctrl = &BB33

km_wait_char = &BB06
km_wait_key = &BB18
km_read_key = &BB1B
km_disarm_break = &BB48

      IMPORT "const.i"  ; for kreturn and co

setup_keys
;---------
; Ensure keys (with or w/o control / shift) are mapped to the codes
; we use for dispatch, for each CPC keyboard en/es/fr

          call km_disarm_break

; --- function keys --------------
; We remap all fn, so there can be caught (e.g. f6/f9 for octave) 
    ; Otherwise would be interpreted as numbers
; We remap all ctrl-fn, even if firmware default.
; We don't know if they have been remapped. 

          ld hl,keyfunc
          ld de,kf*&0100 + kcf
setfun_lp
          ld a,(hl):inc hl
          push hl
          ld c,a
          ld b,d
          call ksetkey
          ld a,c
          ld b,e
          call ksetctrl
          pop hl
          inc d:inc e
          bit 5,e       ; stop at 160 = &a0
          jr z,setfun_lp

; --- key -> code ---------

          ld ix,ksetkey
          call setkeymap

; --- shift+key -> code  ---------

          ld ix,ksetshift
          call setkeymap

; --- ctrl+key -> code  ---------

          ld ix,ksetctrl
          call setkeymap

; --- ctrl for FR/EN/ES ---------

          ld a,29
          call kgetkey
          ld hl,keymap_ctrl_fr:cp "m":jr z,setkeymap
          ld hl,keymap_ctrl_en:cp ":":jr z,setkeymap
          ld hl,keymap_ctrl_es
setkeymap
setkey_lp
          ld a,(hl):inc hl:or a:ret z
          ld b,(hl):inc hl
          push hl
          call jp_ix
          pop hl
          jr setkey_lp

jp_ix     jp ix

; --------------
; Map tables
; !! Must remain contiguous and in this order
; --------------

keyfunc
; codse keyboard f0, f1, ...
      BYTE 15,13,14,5,20
      BYTE 12,4,10,11,3

keymap
      BYTE 68,ktab      ; remapped
      BYTE 7,".",6,&0D
      BYTE 0

keymap_shift
      BYTE 79,ksdel
      BYTE 16,ksclr
      BYTE 0

keymap_ctrl
      BYTE 64,kc1
      BYTE 65,kc2
      BYTE 56,kc4
      BYTE 49,kc5
      BYTE 48,kc6
      BYTE 27,kcp
      BYTE 47,kcspc
      BYTE 18,kcreturn
      BYTE 79,kcdel
      BYTE 28,kcpercent ; ";" on en/es
      BYTE 0

keymap_ctrl_fr
      BYTE 29,kcm
      BYTE 17,kcstar
      BYTE 0

keymap_ctrl_en
      BYTE 38,kcm
      BYTE 29,kcstar
      BYTE 0

keymap_ctrl_es
      BYTE 38,kcm
      BYTE 17,kcstar
      BYTE 0

