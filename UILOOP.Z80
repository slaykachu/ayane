; <<< Common process and *some* global keys (piano) >>>
todo  = 1
; Note: NO LOOP HERE, we actually loop in uientry
      IF todo
; TODO: rename source then?
      END

; 2025 
      ;Aug 03: Display bandaya text
      ;Jul 15: Move instr_ed_entry in uientry.o
             ; Add piano handling

      IMPORT "memmap.i"
      IMPORT "const.i"
      IMPORT "assert.o"
      IMPORT "ged.o"
      IMPORT "piano.o"  ; for /check_note/ (playing key)
      IMPORT "instrui.o" ; for instr_down/instr_ui/instr_change
      IMPORT "disp-jp.o"
      IMPORT "scan-key.o"
      IMPORT "shortcut.o" ; km_wait_key, kesc
; pianoui (octave change) at the end

;---------------------------------------------
uiloop_code
          jr ui_init_band
          jr ui_process_key

;---------------------------------------------
      MACRO LOCATION x,y
          ld de,x*&0100 + y
      ENDM

      MACRO CALL_DS adr
 ; ds="disp status" status_* in disp.o
 ; disp-jp does the connection
      IF BK_DISP - &C7
 !!!! review
      END
          call adr
      ENDM

      MACRO CHECK_UNHANDLED
;NC,Z was reserved by ged (to notify event?)
; But we don't handled that for now
; Z: ev_child_exit. 
          call z,to_be_implemented
      ENDM

      MACRO CALL_PIANO adr
; tst version         
      IF $ AND &4000
 !! mustn't be in bank  
      END
          ld bc,&7FC1:out (c),c
          call adr
;Doesn't need to connect back
      ENDM
;---------------------------------------------

ui_init_band
;-----------
          CALL_PIANO(disp_piano)
; Bandaya fields 
          ld bc,_fields
          ld de,0       ; top left
.flp
          push de
          CALL_DS(status_str_at)
          pop de
          inc e
          ld a,(bc):or a
          jr nz,.flp

          LOCATION(x_pos_instr#,y_pos_instr#)
          ld a,1:CALL_DS(status_deci_a_pad0_at)
          ld a,1:jp instr_change ; update display 

;-------------
ui_process_key
;-------------
; Wait for key and process it
;Out: Carry if key handled
    ; NC if not, A = key/event code 
    ;      ld a,(&4000):cp &C5:call nz,&BE00
          call km_wait_key
     ;     push af
     ;     ld a,(&4000):cp &C5:call nz,&BE00
     ;     pop af
          call gedeihen_process
     ;     push af
     ;     ld a,(&4000):cp &C5:call nz,&BE00
     ;     pop af
          ret c

;--- Unhandled key
          CHECK_UNHANDLED()
          ld c,a        ; needed by scan_key
          ld a,kesc
          cp c
          ret z         ; for uientry to process

          ld hl,shortcuts
          call scan_key
;--- Try piano if no shortcuts found (none for no)
          jr nc,.go_check_note
;--- Otherwise go to shortcut and flag as handled
          call jp_hl
          scf
          ret

.go_check_note          ; Play note if pressed. Return NC otherwise
          push bc
          call check_note
          pop bc:ld a,c ; pop a without touching Carry
          ret

jp_hl     jp hl


octave_down
;TODO: pass octave instead
          CALL_PIANO(octave_lft)
          scf
          ret
 ;todo
octave_up
;TODO: pass octave instead
          CALL_PIANO(octave_rgt)
          scf
          ret

_fields
      BYTE "   '\   / \   |/'>  \__   /''\    --5 Aug 2025-- For Bun",0
      BYTE "  /'''  |__>  /  /  <  |  ,,,/    (f6/f9)  Octave",0
      BYTE "  \,,/     \  <,/|   \ /   \,     (CTRL-I) Instr   :",0
      BYTE 0

shortcuts
      BYTE kf+5:WORD instr_down ; f5
      BYTE kf+8:WORD instr_up ; f8
      BYTE kf+6:WORD octave_down ; f6
      BYTE kf+9:WORD octave_up ; f9  
; Fallback in caller (ayane.o)
      BYTE 0

savepc = $

      BANK &C7
      ORG disp_start'
      ORG $ + &8000,$$  ; &c1
      IMPORT "pianoui.o"
      IF $-&8000 - $$
 !! out of sync
      END

      ORG savepc

