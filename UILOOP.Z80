; <<< Common process and *some* global keys (piano) >>>
todo  = 1
; Note: NO LOOP HERE, we actually loop in ayane.o
      IF todo
; TODO: rename source then?
      END

; 2025    
; -- alpha 1 --
  ; Dec
    ;26 s/CALL_DS/CALL_STATUS (uniformisation)
    ;18 adjust for phrasui integration
; -- v0au --
  ; Nov
    ;ui_init_band: don't change instr! 
; -- v0at --
  ; Oct
    ;15: Move bandtext to doc-in/strings.o

      IMPORT "memmap.i"
      IMPORT "const.i"
      IMPORT "macro.i"
      IMPORT "import.i"

ho
          IMPORT_UILOOP_DEPS()
; Fixed dest, not imported by ayalib
          IMPORT_BANDAYA()
;          BANDAYA_JP()
      BANK BK_DISP
          IMPORT_DISP() ; already imported by instrui and ???
          IMPORT_PIANOUI()
          IMPORT_STRINGS()
hu
          ASSERT($ <= uiloop) ;from ayalib: deps already imported
      ORG uiloop
;---------------------------------------------
uiloop_code
          jr ui_init_ged_null
          jr ui_init_band
          jr ui_process_key

;---------------------------------------------
      MACRO CHECK_UNHANDLED
;NC,Z was reserved by ged (to notify event?)
; But we don't handled that for now
; Z: ev_child_exit. 
          call z,to_be_implemented
      ENDM

      MACRO CALL_PIANO adr
; Cannot ASSERT as labels are forward defined
;  (pianoui imported at the end for some reason. Which one??)
      ;    ASSERT(adr AND &C000 == &C000) ; page c1        
;Needs to connect bk back, otherwise note not played after octave change
          call call_status:WORD adr
      ENDM

;---------------------------------------------
ui_init_ged_null
;---------------
; Needed before we can call ui_process_key
          call gedeihen_init
          ld hl,ui_null
          jp setup_widget

ui_init_band
;-----------          
; Disp piano, set octave and instr.

          CALL_PIANO(disp_piano)
          call octave_sync
; Bandaya fields 
          ld bc,bandfields+&8000
          ld de,0       ; top left
          CALL_STATUS(status_paragraph_at)
; Name                         
          ld bc,name_module+&8000
          LOCATION(x_pos_name,y_pos_name)
          CALL_STATUS(status_str_at)
; Instr
          jp update_instr_in_status

;-------------
ui_process_key
;-------------
; Pass key through gedeihen/fallthrough/piano
; In: A = key code
;Out: Carry if key handled
    ; NC if not, A = key/event code 
          call gedeihen_process
          ret c

;--- Unhandled 
        ;  CHECK_UNHANDLED()
          ld c,a        ; needed by scan_key
          ld hl,shortcuts
          call scan_key
;--- Try piano if no shortcuts found 
          ld a,c
          jr nc,.go_check_note
;--- Otherwise go to shortcut and flag as handled
          call jp_hl
          scf
          ret

.go_check_note          ; Play note if pressed. Return NC otherwise
          push af
          call is_piano_key_pressed ; tmp DI
          jr nc,.nonote

          CALL_PIANO(disp_note)
          pop af        ; discard
;play_note does DI. So it automatically turn off status.
;But then when putting back EI, INTs might be out of sync.
;So we explicitly turn off/on.
;!! copy-pasted in ayane.o (cannot put in macro.i: status_off undefined)
          call status_off
          call play_piano ; will re-read note from keyboard
          ei
          call status_on
          scf
          ret

.nonote
          pop af        ; Return NC + key_code
          ret

jp_hl     jp hl


octave_down
          CALL_PIANO(octave_lft)
          jr octave_sync
octave_up
          CALL_PIANO(octave_rgt)
octave_sync
 ; copy ui to octave in piano 
 ; done here since both piano and pianoui are deps
    ; but neither are deps of each other
; In: A = octave (sent by ui)
;*12 for shift
          ld c,a
          add a:add c
          add a:add a
      IF octave_shift AND &C000 == &4000
 !! ensure to connect right bank   ; For now, out of bank, so ok
      END
          ld (octave_shift),a
          scf           ; needed for ged
          ret

shortcuts
      BYTE kf+5:WORD instr_down ; f5
      BYTE kf+8:WORD instr_up ; f8
      BYTE kf+6:WORD octave_down ; f6
      BYTE kf+9:WORD octave_up ; f9  
      BYTE "B"-&40:WORD to_be_implemented ; for manual check
; Fallback in caller (ayane.o)
      BYTE 0

ui_null
      BYTE w_brk        ; todo: rename to w_null

uiloop_end
      IF 1
          LIMIT(phrase)
; Ensure sync with ayane
          ASSERT(memcpc_code == memcpc)
          ASSERT(scankey_code == scankey)
          ASSERT(conv_code == conv)
          ASSERT(chunk_code == chunk_module)
          ASSERT(instrui_code == instrui)
          ASSERT(field_editor == field_module)
; Ensure sync with phrasui
          ASSERT(instr_code == instr_module)
 ;          ASSERT(ppi_code == ppi)   ; not imported directly
          ASSERT(piano_code == piano)
      END
