; Periodic table of notes!
todo  = 1
need_room = 1

; 2025

      IMPORT "plyconf.i"
      IMPORT "const.i"  ; note_c_10

      IF ayane
      IMPORT "macro.i"
      IMPORT "memmap.i" ; diapason
      IMPORT "memcpc.o" ; scope_bk_base
      END

periods_code

      MACRO INIT_PERIODS
; Fill whole table. 10 octaves + 8 notes. Put 0 at C-10
; Adapted from lab/miniplyi
    ; More compact
; Need to keep working note without rounding
; otherwise error would be propagated.
; Ok    %101 -> %11 (%10.1) ->  %1 (%1.01)
; Wrong %101 -> %11 (%10.1) -% %10 (%1.1)  if we reload from %11

          ld hl,c0
          ld de,c1
.note_lp
          ld c,(hl):inc l
          ld b,(hl):inc l
          push de
.octave_lp
          srl b:rr c
;round to closer
          ld a,c:adc 0
          ld (de),a:inc e
          ld a,b:adc 0
          ld (de),a
          ld a,e:adc 23:ld e,a ; adc for crunch (NC guaranteed)
          jr nc,.octave_lp
          pop de
          inc e
          inc e
          ld a,e
          cp c1+24 AND &FF
          jr c,.note_lp

;C-10: period 0 for percussions.
;C#10 and after: leave as is, nevermind
          ld l,note_c_10*2
          xor a
          ld (hl),a:inc l
          ld (hl),a
      ENDM

      IF ayane
          ASSERT($ == periods_code)
init_periods
          INIT_PERIODS()
          ret

get_diapason
; Out: HL = freq * 128
; Init in init.o
          call scope_bk_base
          ld hl,(diapason)
          ret

set_diapason
; In: hl = freq in 9+7 bits
; Todo: rebuild period table if different!       
   ; for now only one diapasion...

          ld bc,diapason_default
          or a:sbc hl,bc:add hl,bc:call nz,programming_error
      IF need_room
; use generic routine set_a_in_bk_base
      END
          call scope_bk_base
          ld (diapason),hl
          ret
      END

      FILL -$ AND &FF,0

periods
      IF periods AND &FF
 !! must be XX00 aligned  
      END

; Generated by periods.bas
; For x in -57..-46
; f = 440*2^(x/12)
; p = cint(1000000/f)

      IF periods_in_16bits
C     = &EEE4
C#    = &E17C
D     = &D4D4
D#    = &C8E2
E     = &BD9C
F     = &B2F7
F#    = &A8EC
G     = &9F71
G#    = &967E
A     = &8E0C
A#    = &8613
B     = &7E8C
      ELSE
 !! todo
      END

; Align to midi notes. C0 (Double bass pedal) is 12.
      IF 1-ayane
; Room for psg register
!! no, should act the same (for common wrap behavior)
; -> update compile.o for custom psg_regs dest
      END
c0
      WORD C,C#,D,D#,E,F,F#,G,G#,A,A#,B
;      WORD &0EEE,&0E18,&0D4D,&0C8E ; C   C#  D   D#
;      WORD &0BDA,&0B2F,&0A8F,&09F7 ; E   F   F#  G
;      WORD &0968,&08E1,&0861,&07E9 ; G#  A   A#  B
c1
    ; Don't pad, for correct binary size when assembling ply
    ;  FILL -$ AND &FF,0


