; <<< Play song >>>

;2026
 ;--alpha 3--
  ; Feb 4: play_song_cpt: cpt on 16 bits
         ; set_track_stack: more checks
 ;--alpha 2--
  ; Jan 2: play_song_cpt: must scope bk (when called for PHRASUI)
      ; 1: Add set_track_stack for replay from arbitrary point

dev_checks' = 1

      IMPORT "memmap.i"
      IMPORT "macro.i"

.refpc
      IMPORT "memcpc.o" ; scope_bk_base
      IMPORT "ppi.o"
      IMPORT "piano.o"  ; reuse code
      IMPORT "ply.o"
      IMPORT "timer.o"
      IMPORT "assert.o"
          ASSERT($ == .refpc) ; deps must have been imported beforehand

playsong_code
          jr play_song

set_track_stack
;--------------
; In: A = track # (1-based)
    ; B = transpose
    ;HL = pnt in phrase 
    ;DE = delai before row (0 if immediate, ffff if no row)
    ;Amorce_song called
          push hl
          push de
      IF dev_checks'
          or a:call z,mess
          cp 4:call nc,mess ; tmp. TO be replaced by MAX_TRACKS
          ld d,a
          ld a,h:and &C0:cp &40:call nz,mess
          ld a,d
      END
;stack pnt (1 -> track_stack itself)
          ld hl,track_stack+1 - size_per_track
          ld de,size_per_track
.mul      add hl,de
          dec a
          jr nz,.mul
;rdv track event = &ffxx (none as we won't play past row)
          dec a         ; &ff
          ld (hl),a:inc hl ; MSB suffice
;no pnt event then
          inc hl
          inc hl
          ASSERT(track_rdv_row == 4)
          pop de
          ld (hl),e:inc hl
          ld (hl),d:inc hl
          ASSERT(track_phrase_pnt == 6)
          pop de        ; was hl: pnt
          ld (hl),e:inc hl
          ld (hl),d:inc hl
          ASSERT(track_transpose == 9)
          inc hl
          ld (hl),b
;The rest (max vol, instr_null ...) already set by Amorce_song
          ret

play_song
;--------
; In: Amorce_song or set_track_stack called
; Out: N/A (We loop until ESC pressed)
          ld de,0
play_song_cpt
;------------
; DE = nb iter (0 = inf)
; Out: N/A (We loop until ESC pressed or cpt exhausted)
          ld (play_cpt+1),de
          call scope_bk_base ; For return to PHRASUI
          call _prelude
          xor a:ld (.keyprev+1),a ; consider space pressed
          call play     ; prepare regs
          call play_prelude'
          jr .play_song_entry

.play_song_lp
;------------
; Here A=TM  (value doesn't matter for first iteration)
          call vsync_timed
; Must be done at stable position
.play_song_entry
          call set_timer_and_psg

          ex af,af
          ld a,&48:call testkey
          inc c         ; ESC, 1, tab ...
          jp nz,_stop_play
          SUB_TM_CALL(5+17) ; including call vsync_timed

.keyprev  ld de,0       ; only for e
          ex af,af
          ld a,&45:call testkey
;Only stop at pressdown
          ld (.keyprev+1),bc
; anti-key test (otherwise too fast)
          inc e:jr nz,.held
          inc c         ; space, 6, 7, u, y ...
          jp nz,_stop_play
          SUB_TM(4-1)
.held
          SUB_TM_CALL(21)

          call play     ; regs for next frame

          SUB_TM_CALL(5+9+4)
          ex af,af
.play_cpt ld hl,0
          ld a,l:or h:jr z,.ok ; already 0: until key press
          dec hl:ld (.play_cpt+1),hl
          ld a,l:or h
          jp z,_stop_play
          SUB_TM''(12-1)
.ok
          ex af,af
          jr .play_song_lp
hi
