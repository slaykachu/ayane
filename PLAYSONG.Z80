; <<< Play song >>>

      IMPORT "memmap.i"
      IMPORT "macro.i"

.refpc
      IMPORT "ppi.o"
      IMPORT "piano.o"  ; reuse code
      IMPORT "ply.o"
      IMPORT "timer.o"
          ASSERT($ == .refpc) ; deps must have been imported beforehand

play_song
;--------
; In: Amorce_song called
; Out: N/A (We loop until ESC pressed)

          call _prelude
          xor a:ld (.keyprev+1),a ; consider space pressed
          call play_prelude
          call play     ; prepare regs
          call play_prelude'
          jr .play_song_entry

.play_song_lp
;------------
; Here A=TM  (value doesn't matter for first iteration)
          call vsync_timed
; Must be done at stable position
.play_song_entry
          call set_timer_and_psg

          ex af,af
          ld a,&48:call testkey
          inc c         ; ESC, 1, tab ...
          jp nz,_stop_play
          SUB_TM_CALL(5+17) ; including call vsync_timed

.keyprev  ld de,0       ; only for e
          ex af,af
          ld a,&45:call testkey
;Only stop at pressdown
          ld (.keyprev+1),bc
; anti-key test (otherwise too fast)
          inc e:jr nz,.held
          inc c         ; space, 6, 7, u, y ...
          jp nz,_stop_play
          SUB_TM(4-1)
.held
          SUB_TM_CALL(21)

          call play     ; regs for next frame

          SUB_TM_CALL(5+3)
          jr .play_song_lp

