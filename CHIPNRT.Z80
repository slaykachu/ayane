; tests chipnsfx convertion
do_play = 1             ; 0: check  1: play tune
is_minimala = 0         ;0: --4k08-- kim9 (40 years md)

; 2025
  ; Mar
    ; 17: Dump regs in /dbg/

      ORG &0100
      ENT tests

      IMPORT "memmap.i"
      IMPORT "chipn.o"
      IMPORT "init.o"
      IMPORT "ply.o"
      IMPORT "track.o"  ; just for /tracks/!
      IMPORT "testlib.o"

tests
          call full_init
          call test_convert_minimal
          ret

test_convert_minimal
; That's the name of the song.
; Not really minimal!
          ld hl,chp_module
          call conv_chipn
;-- play it
          call Song_get_first_track
          call get_track_header
          ld a,3
          call Amorce_song
      IF 1-do_play
          call chip_init
      END
frame_lp
      IF do_play
          call &BD19
          call &BB1B
          ret c

          halt
          halt
          halt
      END

frame_cpt ld hl,-1      ; matches ply.tick
          inc hl
          ld (frame_cpt+1),hl
      IF is_minimala
          ld bc,&1480
      ELSE
          ld bc,&FFFF
      END
          or a:sbc hl,bc:jr z,end_of_tune

          di
          exx:push bc
          ld bc,&7F10:out (c),c:ld c,&5F:out (c),c
          call play

dbg_pnt   ld de,dbg
          ld hl,psg_regs
          ld b,11
.cp       ld a,(hl):inc hl:ld (de),a:inc e:djnz .cp
          ld (dbg_pnt+1),de

      IF do_play
          ld bc,&7F10:out (c),c:ld c,&40:out (c),c
          call psg
      END
          ld bc,&7F10:out (c),c:ld c,&44:out (c),c
          pop bc:exx

      IF 1-do_play
; Check against chipnsfx player
          ld bc,&7F00+BK_REF:out (c),c
          call chip_play
          ld hl,psg_ref
          call psg_read

;notes: period diff 3 at 683
                  ; 4 at a83

          ld hl,psg_regs
          ld de,psg_ref
          call compare_regs
      END

          jr frame_lp

end_of_tune
; success!
          ret

      FILL -$ AND &FF,0
dbg   SKIP &0100

      IF 1-do_play

      IF is_minimala
chip_player = &5000
chip_setup = chip_player
chip_setup_song = chip_player+&15
chip_play = chip_player+&50
chip_sng = &7000
song_a = chip_sng+&00
song_b = chip_sng+&0175
song_c = chip_sng+&01F8
song_list
      WORD song_a-song_list-2
      WORD song_b-song_list-4
      WORD song_c-song_list-6

chip_init
          di
          ld bc,&7F00+BK_REF:out (c),c

          call chip_setup

          ld hl,song_list
          call chip_setup_song
          ret
      ELSE
chip_player = &4000
chip_play = chip_player+&E2

chip_init
; already pre-init
          ret
      END

      FILL -$ AND &FF,0
psg_ref SKIP 14


BK_REF = &C6

      BANK BK_REF
      IF is_minimala
      ORG chip_player:LOAD "chipnsfx.out"
      ORG chip_sng:LOAD "minimala.bin"
      ELSE
      ORG chip_player:LOAD "sqr.bin"
      END

      END

      BANK BK_IO
      ORG &4000
chp_module
      IF is_minimala
      LOAD "ums:music/tj/minimala.chq" ; with header
      ELSE
;      LOAD "4k08.chq"   ; with header
;      LOAD ":40y/kim9.chq" ; binary version
;      LOAD ":40y/kima.chq" ; binary version
;      LOAD ":40y/kim0.chp"
      LOAD ":40y/kimc.chp"
      END
