; ------------- Tests for instr.o ----------------

; --- alpha1 ---
   ; Dec 26 Add test_non_existing_instr
; --- v0.0at ---
   ; Oct 16 Move "prof.o" at the end to fit in ram.
          ; Use nrt_msb_tm instead of d' (consistent with timernrt & co)
; --- v0.0aq ---
   ; Sep 27 c-0 is now 0
          ; nrt_init: call init_timer
; --- v0.0ac ---
   ; Mar 05
; Changelog in journal.o


      IMPORT "const.i"
      IMPORT "memmap.i" ; psg_regs
      IMPORT "macro.i"  ; SUB_TM

      ORG code_start
      ENT tests

      IMPORT "instr.o"
      IMPORT "timer.o"  ; set_reg13_insert_and_program/_tm init_timer
      IMPORT "testlib.o"
;prof.o at the end

tests
          call nrt_init
          call test_instr_null
          call test_instr
          call test_non_existing_instr
          ret

nrt_init
          call init_for_test
;Clean psg regs since most of them aren't touched
          ld hl,psg_regs
          ld b,15
.raz      ld (hl),0:inc l:djnz .raz
;needed since profile temporarily set EI,
;and we need to setup HL' before that.
          ld hl,&C9FB:ld (&38),hl
          call init_timer ; needed for set_reg13_insert_and_program
          ret

test_instr_null
;--------------
; Check profiling 
          ld hl,instr_null
          ld (instr_code_pnt),hl
          call nrt_instr_play
          ret

test_instr
;--------- 
; Exercise instr_default 
          call dummy_instr

          ld l,1:call instr_setup
;row0  
          ld hl,.ref0
          call check_from_channel_b
;row1
          ld hl,.ref1
          call check_from_channel_b

;row2 More things to check
          ld hl,.ref2
          call check_from_channel_b
; check period
          ld de,&9F71+hard_pitch /256 + 1
          ld hl,(psg_regs+11)
          or a:sbc hl,de:add hl,de:call nz,fail
          ld de,.ref2code
          ld bc,ref2code_
          call check_fx

;Past instr: mustn't touch anything (Future-proof: easy mixing)
; Well, R7 is reset. 
          ld hl,_untouched
          call check_from_channel_b
          ld de,.ref2code
          ld bc,ref2code_
          call check_fx
          ret

      MACRO NRT_CHANB_REF r2_3,r6,r7,r9
      WORD r2_3:FILL 2,nrt_mark:BYTE r6,r7,nrt_mark,r9
      ENDM
.ref0     NRT_CHANB_REF(&EEE4,nrt_mark,&3D,15)
.ref1     NRT_CHANB_REF(&7772,nrt_mark,&3D,13)
.ref2     NRT_CHANB_REF(&EEE4,3,&2D,&10)
.ref2code
          ex af,af
          ld a,sync_env_type
          ld de,&7772+sync_pitch ; c1
          jp set_reg13_insert_and_program
ref2code_ = $ - .ref2code

_untouched              ; only r7 reset (pre_playing)
          NRT_CHANB_REF(nrt_mark*&0101,nrt_mark,&3F,nrt_mark)

test_non_existing_instr
;----------------------             
          ld hl,&BE00:ld (instr_code_pnt),hl
          ld l,2:call instr_setup
          ld hl,_untouched
          call check_from_channel_b
          ret

;-------------------------
nrt_instr_play
          di
          xor a:ld (nrt_msb_tm),a
          exx
          ld hl,timer_nop
          exx
          ex af,af
          ld a,MAX_TM
          ex af,af
          ld hl,instr_play
          call profile
          neg
          add a
          add MAX_TM*2-3 ;-3: final 'ret' not counted by prof
          cp l:call nz,fail
          ld a,(nrt_msb_tm):cp h
          ret


check_from_channel_b
;in: hl = ref
          push hl
; -- clear psg regs for ref check
          ld hl,psg_regs
          ld b,14
nrt_mark = &F7
.raz      ld (hl),nrt_mark:inc l:djnz .raz

          ld a,0        ; c0
          call nrt_instr_play ; on channel B
          pop hl
          ld de,psg_regs+2
          ld bc,8
          call compare_sized
          ret

      IF instr_flags-3
   !! review
      END
      IF instr_vol-5
   !! review
      END

check_fx
; In: DE = pnt ref
    ; BC = size ref
; Just one fx, programed at pos 0 (since current_clock = 0)
; A bit brittle. If break, we might just rely on tests in timernrt.
          ld hl,header_events
          ld a,(hl)
          add hdr_code-hdr_cpt
          ld l,a
          inc h         ; events (but label is ambiguous)
          call compare_sized
          ret

      SKIP ramlimit - $

      ORG free_ram_for_nrt
      IMPORT ":orgrel/prof.o"

