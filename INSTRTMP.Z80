todo  = 1
may_optim = 1
may_crunch = 1

; Routine for each type of intruments:
      ; - instr_default
      ; - instr_chip_down_vib
      ; - instr_chip_down_vib_noise
      ; - instr_chip_down_arp
      ; - instr_chip_down_arp_noise

; Limitations:
   ; - only works for fixed /regs/ address XX00. 

      IMPORT "plyconf.i"

dev_checks = 0 AND ayane ; deactivate: otherwise TM doesn't fit
;-> not really true anymore as of 03 Mar 2025 as we added /SUB_TM_CALL/

  IF ayane
      IMPORT "const.i"  ; instr_head_size
      IMPORT "memmap.i" ; BK_INSTR, instr_idx, psg_regs
      IMPORT "periods.o"
;!! periods don't pad itself, to get the correct binary size
;!! when assembling ply.o with ayane=0
      FILL -$ AND &FF,&F7
      IF sfx
      IMPORT "timer.o"  ; for ret_sp
      END
      ELSE
psg_regs = compiled_psg_regs
 ; instr is imported at start of ply.o (to ensure confinement)
 ; yet periods must go in its own &100 buffer at the end of code
      IF $ - $$
  !! unexpected
      END
save_org = $
      ORG compiled_periods
      IMPORT "periods.o"
      ORG save_org
      END

r6    = psg_regs+6
r7    = psg_regs+7
r11_12 = psg_regs+11
r13   = psg_regs+13
r14   = psg_regs+14     ; Reset by ply before iteration

      IF ayane
      IF $ AND &FF:ELSE
; LSB cannot be null: 00 used to flag empty instr.
; Hmm... we could use instr_null instead? nevermind.
; For compiled version: no such issue!
          nop
      END
      END

  IF may_optim
; chip+arp : don't read arp first iter?
   ; - faster
   ; - problem: then doesn't match chipsnf
              ; -> actually can be mitigated by shifting table
      END

; \\\\\ 2025 ///
;Mar
; ---- v0l2 ----
  ; SIGN: only for ayane = 1
  ; Use default_use_arp
      ; default_use_hard_env
  ; Fix instr_default for kim compiled

; \\\\\ 2024 ///
; Jun
; ---- v0l ----
  ; 22 Add param# in signature
  ; 11 Put signature before each instr
     ; Reorder code so all amorces fit in &100 
            ; Plus, compress better:
               ; + similar code together
               ; - JPs instead or similar JRs
                   ; -> This one can change if we handle noise first
                      ; and factorise vib/vib_noise (same for arp) 
; ---- v0k ----
  ;  3 Fix LSB static checks (handle when instr no assembled)
     ; enable /instr_chip_down_arp/
; May
  ; 20 Add variants:
           ; - instr_chip_down_arp
           ; - instr_chip_down_vib_noise
     ; Make instr_state_pnt a variable for better crunching
; ---- v0h ----
  ; 14 Fix 'table link' in compiled version
     ; Optimise instr_chip_down_vib (no noise: no need to touch r7)
; Apr            
  ; 29 Add instr_chip_down_arp_noise
; ---- v0g ----
  ; 22 Don't handle volume if use_volume = 0
  ; 14 Add instr_null
  ; 11 Filter loop bit flag
     ; instr_chip_down_vib: fix reg 7 handling

; Mar 
  ;31: instr_chip_down_vib: proper init
  ; 30: Add amorce code
      ; Use INC HL as rows aren't confined anymore.
        ; We might revisit that if we really need speed.
      ; Optimise instr_default!
; Madram 25 Feb 2024. v0

loop_bit = 7
fail  = &BE00

     IF dev_checks
 !!! no, doesn't fit in TM
      END

      MACRO EX_AF'
      IF sfx
          ex af,af
      END
      ENDM

      MACRO SUB_TM n
; Copy-paste from timer.o                  
      IF sfx
; Update counter TM (+2 for 'sub n' itself)
      IF n AND 1
; Resolution: 2-nops. Must compensate
          ld a,a:sub [n+3]/2
      ELSE
          sub [n+2]/2
      END
      END
      ENDM

      MACRO SUB_TM'' tm
      IF sfx
          ex af,af
          SUB_TM(tm+2)  ; +2 for "ex af,af"
          ex af,af
      END
      ENDM

      MACRO SUB_TM_CALL n
; Update and call HL' when CPT < 0 
          SUB_TM(n+9)
; Cannot use call m,jp_hl', as it must take the same time in both case
          call jp_m_hl'
      ENDM

      MACRO RET_ROUT tm
      IF sfx
          SUB_TM(tm+4)
          ret p
          exx:jp hl
      ELSE
          ret
      END
      ENDM

      MACRO RET_ROUT' tm
          EX_AF'()
          RET_ROUT(tm+1)
      ENDM


      MACRO CHECK_CONFINE
; For now constraint also in-tool
; Can aleviate that by storing ID instead of LSB
; Other mitigation possible: put instr_default last
      IF $/&0100 - instr_code/&0100
  !!! error confine 
      END
      ENDM

type_instr_null = 0
type_instr_mute = 1     ; todo 
type_instr_default = 2

instr_code              ; MSB needed by ply.o

VARIABLE_SIZE = -1      ; For rows

      MACRO SIGN type,paramsize,rowsize
      IF ayane
; Order used by chung.Song_select_instr
      IF paramsize AND &FC ; >3
  !! for more than 3 params, make sure to export them
  ; as the are only 3 byte free in the header
      END

      BYTE rowsize      ; 0: no table associated. -1: variable
      BYTE paramsize
      BYTE type
      END
      ENDM

          SIGN(type_instr_null,0,0)
instr_null
          pop hl        ; consume param
          pop hl        ; consume regs+vol
;Actually instr_mute:
 ; we set vol to 0 for minimala.chp: allows to open all channels
 ; inconditionnaly
; !!! NO !!! -> Lead to a bug, since now compile.o replace intermediary
; dummy row (for duration > &100) by instr_null (to avoid having to 
; deal with flags). So for minimala.chp it would cut the sound at
; iter &101 (i.e tick +1 from 2nd "pattern")
; Instead, we rely on regs 8,9,10 being 0 at init time
;          ld (hl),0                      
          pop hl        ; consume regs+vol            
          RET_ROUT(9)

instr_end
; After final row: mustn't touch anything.
   ; - Time efficient for buzzs
   ; - Needed for future: track mix
; Routine placed here to be reachable by jr
tm1_  = 8               ; from .exec to jr instr_end (taken)
          pop hl        ; consume regs+vol
          pop hl        ; consume regs+mask            
          RET_ROUT(tm1_+6)

      IF ayane
; Not used to compile minimala.chp
          SIGN(type_instr_default,0,VARIABLE_SIZE)
instr_default
; Same API than /.exec/
          CHECK_CONFINE()
; First iteration
      IF ayane
 ; replace jp for next iterations
          EX_AF'()
          ld hl,.exec:push hl:pop hl
 ; get row pnt from header.
          pop hl
          ld a,(hl):inc hl ; !! cannot use E (= note)
          ld h,(hl):ld l,a
      IF dev_checks
; If wrong, typically BK_INSTR wasn't connected
          ld a,h:cp tables/&0100:call c,fail
          cp tables_/&0100:call nc,fail
; no loop in header.
          bit loop_bit,h:call nz,fail
          SUB_TM''(18)
      END
          EX_AF'()
          SUB_TM_CALL(26) ; from instr_default until .exec
          push hl
;enchaine with .exec
.exec
      ELSE
;compiled: no indirection
 !! must handle instrument end then.
      END
; In:               
      IF use_volume
      ;  B = Volume (row mitigated by general track volume)
      END
      ;  C = Note (transposed)
      ; DE = accumulated pitch
      ; SP = Points on: - instr parameter
                      ; - Pnt to volume register (eg regs+8)
                      ; - Regs + channel masked (eg %110110 for A)
      IF todo
; Check if af' here
      END
          pop hl        ; Instr table
      IF may_optim
; For chipnsfx version: no need to save and use B
      END
; Follow next link.
; Handling that first allows to skip optional field at the end for free
                          ; and use L or H to read last 
          ld a,c        ; free C
      IF use_volume
          ld ixl,b      ; Free B
      END
          ld c,(hl):inc hl
          ld b,(hl):inc hl
      IF ayane
          res loop_bit,b
      END
          push bc       ; for next time
          pop bc        ; skip

; Start by arpeggio and pitch, since we must do that anyway
; Except if absolute value, but:
   ; - We don't handle those anyway :)
          add (hl):inc hl
; Compute period now:
   ; - optimised since we read pitch anyway
   ; - allow to free C
          ld c,a
          ld b,periods/&0100
; Keep in DE for subsequent fx  
      IF use_phrase_pitch
; DE= pitch set by fx
          ld a,(bc):add e:jr nc,$+3:inc d
          add (hl):inc hl:ld e,a
          inc b
          ld a,(bc):adc d
          add (hl):inc hl:ld d,a
      ELSE
          ld a,(bc):add (hl):inc hl:ld e,a
          inc c
          ld a,(bc):adc (hl):inc hl:ld d,a
      END

      IF use_flag_channel
; Flags channel/noise (copied in each channel)
          ld c,(hl):inc hl
; Note: we must test to prevent changing R6 from tracks
      ; that don't emit noise
          bit 3,c       ; any bit 3,4,5 would work
          jr nz,.ok_noise
          ld a,(hl)
          ld (psg_regs+6),a
      IF todo
 ; update tm or compensate branch.
      END
      ELSE
; Infer noise flag from noise value 
          ld c,&3F
          ld a,(hl)
          or a
          jr z,.ok_noise
          ld (psg_regs+6),a
          ld c,&07
      END
.ok_noise
          inc hl

; Volume, or env_hard:
    ; xxxx0 -> vol xxxx (F=max volume)
    ; yyyy1 -> env hard            
          ld a,(hl)
      IF use_hard_env
          rrca
          jr c,.hard_env
      END
      IF use_volume
          sub ixl:jr nc,$+3:xor a ; a= clipped volume
      END

 ; volume register
          pop hl
          ld (hl),a

 ; period register
          ld a,l:sub 8:add a:ld l,a
      IF increased_precision
 !!! todo
      END
          ld (hl),e:inc l
          ld (hl),d

 ; r7                     
          pop hl
          ld a,c
          or l
          ld l,7
          and (hl)
          ld (hl),a

      IF todo
; handle tm
      END
          ret

      IF use_hard_env
.hard_env
          inc hl
      BRK
  ; todo
; TODO: flags retrig. YAGNI YET
      END
      END

      MACRO CHIP_VOL_AND_PNT
; Update volume and get table pnt
  ; Common for instr_chip_down_arp_noise
             ; instr_chip_down_vib
      IF use_volume
; phrase volume affecting initial value
      ELSE
          pop hl
; instr params
      IF may_optim
; Interleave for compiled version, as MSB is constant? 
; Better: MSB could be static if all chipnsfx instruments fit in &100
; We just need to know destination adress.
      END
; no volume: we can use B
          ld b,(hl):inc l ;step       
          ld a,(hl)     ;current volume
          sub b:jr nc,$+3:add b ; decrement with saturation
          ld (hl),a:inc l
      IF use_phrase_pitch
!! todo 
; Cannot use DE anymore
      ELSE
      IF ayane
          ld e,(hl):inc l ; LSB first to respect common links used
          ld d,(hl)
          dec l         ; for update    
      ELSE
      IF hack_minimala
 ; MSB is constant global. We didn't store it.
      IF may_crunch
; Actually it might be more crunchable to store D anyway and re-read it
; (no "magic" value), as DE is already pointing the table
; when state is setup
      END
          ld e,(hl)
          ld d,compiled_instr/&0100
      ELSE
 ; MSB is constant per instr (so, read it first to ease update).
  !! check me
          ld d,(hl):inc l
          ld e,(hl)
      END
      END
      END
      END
      ENDM

      MACRO CHIP_UPDATE_PNT
; -- update link for next time
      IF ayane
; absolute link. (hl) = (de)
          ld b,a
          ld a,(de):inc e
          ld (hl),a:inc l
          ld a,(de):inc e
          res loop_bit,a
          ld (hl),a:inc l
          ld a,b
; Don't need hl = params anymore
      ELSE
; in compiled version, MSB is constant and link is relative
; (for crunchability)
          ld b,a        ; save
          ld a,(de):add e
          ld (hl),a
          inc e
          ld a,b
      END
      ENDM



instr_chip_down_vib
; Very ad-hoc instrument that matchs mimimala.chp needs
; Same input as inst_default

          ld hl,icdv_exec:push hl:pop hl ; for next iter
          pop hl        ; instr data
      IF ayane
      IF use_phrase_pitch
 !!! todo 
      ELSE
; we can use de
          ld e,(hl):inc hl
          ld d,(hl):inc hl
          ld b,(hl):inc hl ;step
          ld a,(hl)     ;initial volume
      END
      ELSE
; Compiled version: no indirection, rows follow volum parameter
          ld b,(hl):inc l ;step
          ld a,(hl):inc l ;initial volume
          ex de,hl      ; DE points to table
      END
; state needs 2 words, so we deroute in fresh allocation 
instr_state_pnt ld hl,instr_state
          2 ** inc l
          push hl       ; update param
          pop hl
          ld (hl),b:inc l
          ld (hl),a:inc l
          ld (instr_state_pnt+1),hl
          jr icdv_entry

icdv_exec
          CHIP_VOL_AND_PNT()
icdv_entry
; Here: like icdan_entry
          CHIP_UPDATE_PNT()

 ; volume register
          pop hl
          4 ** rrca:and &0F
          ld (hl),a

 ; period register
          ld a,l:sub 8:add a:ld l,a
;!!! doesn't keep6 de=period like instr_default
          ex de,hl
          ld b,periods/&0100
          ld a,(bc):add (hl):inc hl:ld (de),a:inc e
          inc c
          ld a,(bc):adc (hl):ld (de),a

; r7
; Open channel. 
          pop hl
      IF hack_minimala
; Nothing to do (must still consume HL to advance in stack)
      ELSE
          ld a,&38      ; close noise
          or l
          ld l,7
          and (hl)
          ld (hl),a
      END
          ret

      MACRO CONSTANT_NOISE
;!! Hack: we know it is constant for the duration of the instrument 
;!!!! **AND** that other instruments doesn't touch r6.
          ld a,(hl):inc hl
          ld (psg_regs+6),a
      ENDM

instr_chip_down_arp_noise
; Very ad-hoc instrument that matchs mimimala.chp needs:
   ; Instr 4, 5 7 with arp + constant noise (might be 0)
; Same input as inst_default

          ld hl,icdan_exec:push hl:pop hl ; for next iter
; state needs 2 words, so we deroute in fresh allocation 

      IF use_phrase_pitch
!! todo 
; cannot use de anymore
      ELSE
          pop hl
; we can use de since no pitch FX coming for phrase
      IF ayane
          ld e,(hl):inc hl
          ld d,(hl):inc hl
          CONSTANT_NOISE()
          ld b,(hl):inc hl ;step
          ld a,(hl)     ;initial volume
      ELSE
; Compiled version: no indirection, rows follow volume parameters
          CONSTANT_NOISE()
          ld b,(hl):inc l ;step
          ld a,(hl):inc l ;initial volume
          ex de,hl
      END
      END
          ld hl,(instr_state_pnt+1)
          2 ** inc l
          push hl       ; update param
          pop hl
          ld (hl),b:inc l
          ld (hl),a:inc l
          ld (instr_state_pnt+1),hl
          jr icdan_entry

icdan_exec
          CHIP_VOL_AND_PNT()
icdan_entry
; Here a= vol, c= note
     ; hl: field in state: link to table
     ; de: pnt table 
          CHIP_UPDATE_PNT()

 ; -- set volume register
          pop hl
          4 ** rrca:and &0F
          ld (hl),a
 ; -- set period register
          ld a,l:sub 8:add a:ld l,a
;!!! doesn't keep de=period like instr_default
          ld a,(de):add c:ld c,a ; arp
          ld b,periods/&0100
          ld a,(bc):ld (hl),a:inc l
          inc c
          ld a,(bc):ld (hl),a

; -- set r7
; Open channel and optionnally noise.
          pop hl
          ld a,(psg_regs+6)
          or a
          ld a,&38      ; noise close
          jr z,$+3:xor a ; noise open if r6 <> 0
          or l
          ld l,7
          and (hl)
          ld (hl),a
          ret


hi
      IF instr_chip_down_vib/&0100 - instr_code/&0100
  !!! error confine 
      END

      IF ayane
      FILL -$ AND &FF,&F7
instr_state
      FILL &0100,&F7
      ELSE
instr_state = compiled_instr_buf
      END
